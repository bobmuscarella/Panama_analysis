 model {
      
      for (i in 1:N){
      predict.log.growth[i] <- b0[sp[i]] 
      + b1[sp[i]] * log.nci[i] 
      + b2[sp[i]] * log.dbh[i] 
      + i.tree[tree[i]]
      
      true.log.growth[i] ~ dnorm(predict.log.growth[i], process.tau)
      true.growth[i] <- exp(true.log.growth[i])
      
      pred.growth[i] ~ dnormmix(mu[1:2, i], tau[1:2, i], f)
      obs.growth[i] ~ dnormmix(mu[1:2, i], tau[1:2, i], f)
      mu[1,i] <- true.growth[i]
      mu[2,i] <- true.growth[i]
      
      sd1[i] <- (0.927 + 0.0038 * (dbh[i] + 15)) * 1.414 / days[i]
      #    sd1[i] <- (0.927 + 0.0038 * (dbh[i] - 45)) * 1.414 / days[i]
      tau[1,i] <- pow(sd1[i], -2)
      sd2[i] <- 25.6 * 1.414 / days[i]
      tau[2,i] <- pow(sd2[i], -2)
      }
      
      for (j in 1:n.sp){
      b0[j] ~ dnorm(b0.overall + (b0.wd * tmeans.z[j,1]) + (b0.lma * tmeans.z[j,2]), b0.sp.prec)
      b1[j] ~ dnorm(b1.overall + (b1.wd * tmeans.z[j,1]) + (b1.lma * tmeans.z[j,2]), b1.sp.prec)
      b2[j] ~ dnorm(b2.overall, b2.sp.prec)
      
      # b0[j] <- b0.overall + (b0.wd * tmeans.z[j,1]) + (b0.lma * tmeans.z[j,2]) + b0.sp[j]
      # b1[j] <- b1.overall + (b1.wd * tmeans.z[j,1]) + (b1.lma * tmeans.z[j,2]) + b1.sp[j]
      # b2[j] <- b2.overall + b2.sp[j]
      # 
      # b0.sp[j] ~ dnorm(0, b0.sp.prec)
      # b1.sp[j] ~ dnorm(0, b1.sp.prec)
      # b2.sp[j] ~ dnorm(0, b2.sp.prec)
      }
      
      ### prior and random effect ##########
      f[1] <- 0.9724
      f[2] <- 0.0276
      
      for (i in 1:n.tree){
      i.tree[i] ~ dnorm(0, i.tree.prec)
      }
      
      process.tau ~ dunif(0.0001, 10000)
      i.tree.prec ~ dunif(0.0001, 10000)
      
      b0.sp.prec ~ dunif(0.0001, 10000)
      b1.sp.prec ~ dunif(0.0001, 10000)
      b2.sp.prec ~ dunif(0.0001, 10000)
      
      b0.overall ~ dnorm(0, 1.0E-6)
      b1.overall ~ dnorm(0, 1.0E-6)
      b2.overall ~ dnorm(0, 1.0E-6)
      
      b0.wd ~ dnorm(0, 1.0E-6)
      b1.wd ~ dnorm(0, 1.0E-6)
      b0.lma ~ dnorm(0, 1.0E-6)
      b1.lma ~ dnorm(0, 1.0E-6)
      
}
