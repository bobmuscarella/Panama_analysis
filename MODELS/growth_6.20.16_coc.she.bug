 model {
      
      for( i in 1:ntree ) {
      
      growth[i] ~ dnorm(mu[i], tau[1])
      
      y.rep[i] ~ dnorm(mu[i], tau[1])
      sqdiff[i] <- pow(growth[i] - y.rep[i], 2)
      
      mu[i] <- beta.1[species[i]]
      + beta.2[species[i]] * allnci[i]
      + beta.3[species[i]] * dbh[i]
      + indiv.effect[indiv[i]] * indicator[i]
      }
      
        for( j in 1:nspecies ) {
          beta.1[j] ~ dnorm(mu.beta[1] + (beta.wd[1] * t.pred[j,1]) + (beta.lma[1] * t.pred[j,2]), tau[2])
          beta.2[j] ~ dnorm(mu.beta[2] + (beta.wd[2] * t.pred[j,1]) + (beta.lma[2] * t.pred[j,2]), tau[3])
          beta.3[j] ~ dnorm(mu.beta[3], tau[4])

      t.pred[j,1:2] ~ dmnorm(pred.tmeans[j,], omega[,])

        for (N in 1:2){
          pred.tmeans[j,N] ~ dnorm(tmeans.z[j,N], pred.tau[j,N])
          pred.tau[j,N] ~ dgamma(sh[j,N], ra[j,N])
          ra[j,N] <- (ttaus.z[j,N] + sqrt(ttaus.z[j,N]^2 + 4*sd[N]^2))/(2*sd[N]^2)
          sh[j,N] <- (1 + ttaus.z[j,N]*ra[j,N])
        }
      }
      
      ### PRIORS ####
      for( m in 1:3 ) {
      mu.beta[m] ~ dnorm(0, 1E-3)
      }
      
      for( b in 1:2 ) {
      beta.wd[b] ~ dnorm(0, 1E-3)
      beta.lma[b] ~ dnorm(0, 1E-3)
      sd[b] ~ dgamma(1E-3, 1E-3) #dunif(0, 100)
      }
      
      for( i.a in 1:nindiv ) {
      indiv.effect[i.a] ~ dnorm(0, tau[5])
      }
      
      for( t in 1:5 ) {
      tau[t] ~ dgamma(1E-3, 1E-3)
      }
      
      ### TRANSFORMATIONS ###
      Dsum <- sum(sqdiff)
      SStot <- sum(sqdiff)
      r2 <- (1 - (Dsum/SStot))

      sigma <- 1 / sqrt(tau)
      pred.sigma <- 1 / sqrt(pred.tau)
      
      }
