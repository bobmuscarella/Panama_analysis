 model {
      
      for (i in 1:N){
      
      obs.growth[i] ~ dnorm(predict.growth[i], tau[1])
      
      predict.growth[i] <- exp(predict.log.growth[i])
      
      predict.log.growth[i] <- b0[sp[i]] 
      + b1[sp[i]] * log.nci[i] 
      + b2[sp[i]] * log.dbh[i]
      + indiv.effect[tree[i]] * indicator[i]
      }

      for( j in 1:n.sp ) {
      b0[j] ~ dnorm(mu.beta[1] + (beta.wd[1] * tmeans.z[j,1]) + (beta.lma[1] * tmeans.z[j,2]), tau[2])
      b1[j] ~ dnorm(mu.beta[2] + (beta.wd[2] * tmeans.z[j,1]) + (beta.lma[2] * tmeans.z[j,2]), tau[3])
      b2[j] ~ dnorm(mu.beta[3], tau[4])
      }
      
      ### prior and random effect ##########
      for( i.a in 1:n.tree ) {
      indiv.effect[i.a] ~ dnorm(0, tau[5])
      }
      
      for( t in 1:5 ) {
      tau[t] ~ dgamma(1E-3, 1E-3)
      }
      
      for( m in 1:3 ) {
      mu.beta[m] ~ dnorm(0, 1E-3)
      }
      
      for( b in 1:2 ) {
      beta.wd[b] ~ dnorm(0, 1E-3)
      beta.lma[b] ~ dnorm(0, 1E-3)
      }
      
}
