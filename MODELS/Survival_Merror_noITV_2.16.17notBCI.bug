model{
        for (i in 1:N){

        alive[i] ~ dbern(t[i])
      
        t[i] <- pow(z[i], days[i]/365.25)

        logit(z[i]) <- b0[sp[i]] 
        + b1[sp[i]] * log.nci[i] 
        + b2[sp[i]] * log.dbh[i] 
        + indiv.effect[tree[i]]
        }
        
        for( j in 1:n.sp ) {
        b0[j] ~ dnorm(mu.beta[1] + (beta.wd[1] * tmeans.z[j,1]) + (beta.lma[1] * tmeans.z[j,2]), tau[1])
        b1[j] ~ dnorm(mu.beta[2] + (beta.wd[2] * tmeans.z[j,1]) + (beta.lma[2] * tmeans.z[j,2]), tau[2])
        b2[j] ~ dnorm(mu.beta[3], tau[3])
        }

        ### prior and random effect ##########
        for( i.a in 1:n.tree ) {
        indiv.effect[i.a] ~ dnorm(0, tau[4])
        }
        
        for( t in 1:4 ) {
        tau[t] ~ dgamma(1E-3, 1E-3)
        }
        
        for( m in 1:3 ) {
        mu.beta[m] ~ dnorm(0, 1E-3)
        }
        
        for( b in 1:2 ) {
        beta.wd[b] ~ dnorm(0, 1E-3)
        beta.lma[b] ~ dnorm(0, 1E-3)
        }
  }
